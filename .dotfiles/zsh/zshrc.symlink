# uncomment this and the last line for zprof info
# zmodload zsh/zprof

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/Code"

# your default editor
export EDITOR='vim'
export VEDITOR='code'
export VSCODE='code'

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# Set up zinit
source ~/.zinit/bin/zinit.zsh

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

zinit ice atclone"dircolors -b LS_COLORS > clrs.zsh" \
    atpull'%atclone' pick"clrs.zsh" nocompile'!' \
    atload'zstyle ":completion:*" list-colors "${(s.:.)LS_COLORS}"'
zinit light trapd00r/LS_COLORS

zinit wait lucid for \
    OMZP::colored-man-pages \
    OMZP::command-not-found \
    OMZP::dotenv \
    OMZP::helm \
    OMZP::httpie \
    OMZP::kubectl \
    OMZP::oc \
    OMZP::vscode \
    OMZP::zsh-interactive-cd

zinit wait lucid for \
    caarlos0/zsh-mkc \
    djui/alias-tips \
    zpm-zsh/undollar \
  atload"_zsh_autosuggest_start" \
    zsh-users/zsh-autosuggestions \
  atinit"zicompinit; zicdreplay" \
    zdharma/fast-syntax-highlighting \
    zsh-users/zsh-history-substring-search

zinit wait lucid atload"zicompinit; zicdreplay" \
  atpull'zinit creinstall -q .' blockf for \
    zsh-users/zsh-completions

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

zicompinit; zicdreplay

unset config_files

eval "$(starship init zsh)"

# use .localrc or .zshrc.local for SUPER SECRET CRAP
# that you don't want in your public, versioned repo.
# shellcheck disable=SC1090
[[ -f ~/.localrc ]] && . ~/.localrc
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

# zprof
export PATH="/usr/local/opt/curl/bin:$PATH"
