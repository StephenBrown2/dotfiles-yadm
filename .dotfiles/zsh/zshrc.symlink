# uncomment this and the last line for zprof info
# zmodload zsh/zprof

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/Code"

# your default editor
export EDITOR='vim'
export VEDITOR='code-insiders'
export VSCODE='code-insiders'

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

autoload -Uz compinit
if [[ -n ~/.zcompdump(#qN.mh+24) ]]; then
  compinit;
else
  compinit -C;
fi;
# for dump in ~/.zcompdump(N.mh+24); do
#   compinit
# done
# compinit -C

# load antibody plugins compiled with:
function rebuild_antibody(){
  antibody bundle <"$DOTFILES/antibody/plugins.txt" >~/.zsh_plugins.sh
  antibody update
}
source ~/.zsh_plugins.sh

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

eval "$(starship init zsh)"

# use .localrc or .zshrc.local for SUPER SECRET CRAP
# that you don't want in your public, versioned repo.
# shellcheck disable=SC1090
[[ -f ~/.localrc ]] && . ~/.localrc
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

# zprof